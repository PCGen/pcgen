import java.time.LocalDateTime

/*
 * PCGen autobuild generation tasks. This file specifies the way in which the
 * autobuild site is generated and the outputs from other tasks that are to be
 * deployed with the site.
 *
 * Usage: ./gradlew deployAutobuild
 */
ext {
    if (System.env.BUILD_NUMBER) {
        destAutobuildDir = file('/home/pcgen1/public_html/autobuilds/')
    } else {
        destAutobuildDir = layout.buildDirectory.dir("autobuilds").get()
    }

    buildTimestamp = LocalDateTime.now(Clock.systemUTC())
}

tasks.register("copyDocs", Sync) {
    from 'docs'
    into destAutobuildDir.dir("pcgen-docs/")
}

tasks.register("copyDist", Sync) {
    dependsOn buildDist
    from layout.buildDirectory.dir("distributions")
    into destAutobuildDir.dir("downloads")
}

tasks.register("copySite", Copy) {
    // Ensure this always runs - we don't want an incorrect date
    outputs.upToDateWhen { false }
    from layout.projectDirectory.dir("code/gradle/site")
    into destAutobuildDir
    expand(copyright: Calendar.instance.get(Calendar.YEAR), version: version, timestamp: buildTimestamp,
        simpleVersion: version.replaceAll('-SNAPSHOT', '').replaceAll(/\./, ''))
}

tasks.register("copyInstaller", Copy) {
    // Ensure this always runs - we don't want an outdated installer
    outputs.upToDateWhen { false }
    from "${releaseDir}"
    into destAutobuildDir.dir("downloads")
}

// The Main Task
tasks.register("deployAutobuild") {
    dependsOn copyDocs, copyDist, copySite, copyInstaller
}
