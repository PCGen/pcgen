import java.time.LocalDateTime

/*
 * PCGen autobuild generation tasks. This file specifies the way in which the
 * autobuild site is generated and the outputs from other tasks that are to be
 * deployed with the site.
 *
 * Usage: gradle deployAutobuild
 */
ext {
    if (System.env.BUILD_NUMBER) {
        destAutobuildDir = '/home/pcgen1/public_html/autobuilds/'
    } else {
        destAutobuildDir = "${buildDir}/autobuilds/"
    }

    buildTimestamp = LocalDateTime.now(Clock.systemUTC())
}

task copyDocs(type: Sync) {
    from 'docs'
    into "${destAutobuildDir}/pcgen-docs/"
}

task copyDist(type: Sync, dependsOn: 'buildDist') {
    from "${buildDir}/distributions"
    into "${destAutobuildDir}/downloads/"
}

task copySite(type: Copy) {
    // Ensure this always runs - we don't want an incorrect date
    outputs.upToDateWhen { false }
    from "${projectDir}/code/gradle/site"
    into "${destAutobuildDir}"
    expand(copyright: Calendar.instance.get(Calendar.YEAR), version: version, timestamp: buildTimestamp,
        simpleVersion: version.replaceAll('-SNAPSHOT', '').replaceAll(/\./, ''))
}

task copyInstaller(type: Copy) {
    // Ensure this always runs - we don't want an outdated installer
    outputs.upToDateWhen { false }
    from "${releaseDir}"
    into "${destAutobuildDir}/downloads/"
}

task deployAutobuild (dependsOn: ['copyDocs', 'copyDist', 'copySite', \
'copyInstaller']) {
}
