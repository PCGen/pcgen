/* PCGen code quality tasks. This file specifies the code quality
 * reports to be run as part of the PCGen nightly build. It is
 * called from the main build.gradle file.
 *
 * Usage: ./gradlew allReports
 */

checkstyle {
    configFile = new File('code/standards/checkstyle.xml')
    configProperties = [samedir: "${rootDir}/code/standards"]
    showViolations = true
    toolVersion = '10.15.0'
    sourceSets = []
}

pmd {
    // turn off the default rules
    ruleSets = []
    ruleSetFiles = files('code/standards/ruleset.xml')
    consoleOutput = true
    sourceSets = []
    toolVersion = "7.0.0"
    incrementalAnalysis = true
}

// TODO fix the outstanding bugs and then remove the ignoreFailures
spotbugs {
    def classLoader = plugins["com.github.spotbugs"].class.classLoader
    def SpotBugsConfidence = classLoader.findLoadedClass("com.github.spotbugs.snom.Confidence")

    toolVersion = '4.8.3'
    excludeFilter = file("$rootProject.projectDir/code/standards/spotbugs_ignore.xml")
    omitVisitors = ['Naming', 'CrossSiteScripting', 'DontUseEnum', 'DoInsideDoPrivileged']
    reportLevel = SpotBugsConfidence.LOW
    ignoreFailures = true
}

spotbugsMain {
    reports {
        xml.required = false
        html.required = true
        html.stylesheet resources.text.fromFile('code/standards/fancy-hist.xsl')
    }
}

tasks.register('allReports') {
    dependsOn = ['checkstyleMain', 'pmdMain', 'spotbugsMain']
}
