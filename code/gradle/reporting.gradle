/* PCGen code quality tasks. This file specifies the code quality
 * reports to be run as part of the PCGen nightly build. It is
 * called from the main build.gradle file.
 *
 * Usage: gradle allReports
 */

checkstyle {
    configFile = new File('code/standards/checkstyle.xml')
    configProperties = [samedir: "${rootDir}/code/standards"]
    showViolations = true
    toolVersion = '8.44'
    sourceSets = []
}

pmd {
    // turn off the default rules
    ruleSets = []
    ruleSetFiles = files('code/standards/ruleset.xml')
    consoleOutput = true
    sourceSets = []
    toolVersion = "6.21.0"
    incrementalAnalysis = true
}

// TODO fix the 52 outstanding bugs and then remove the ignoreFailures
spotbugs {
    toolVersion = '4.4.0'
    excludeFilter = file("$rootProject.projectDir/code/standards/spotbugs_ignore.xml")
    omitVisitors = ['Naming', 'CrossSiteScripting', 'DontUseEnum', 'DoInsideDoPrivileged']
    reportLevel = 'low'
    ignoreFailures = true
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
        html.stylesheet resources.text.fromFile('code/standards/fancy-hist.xsl')
    }
}
task allReports {
    dependsOn = ['checkstyleMain', 'pmdMain', 'spotbugsMain']
}
