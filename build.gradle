/*
 * PCGen build using the Gradle tool. This file specifies the core build tasks
 * and refers to other files in the code/gradle directory for additional tasks
 * for specific output.
 *
 * Developer build: gradle
 * Incremental dev build: gradle build
 * Full build: gradle all
 * Build and run all tests: gradle clean build slowtest
 * Run the data tests: gradle datatest
 * Run the character integration tests: gradle inttest
 *
 * Author: James Dempsey
 */

plugins {
    id 'application'
    id 'build-dashboard'
    id 'checkstyle'
    id 'findbugs'
    id 'java'
    id 'maven'
    id 'pmd'

    id "de.undercouch.download" version "3.1.1"
    id 'edu.sc.seis.launch4j' version '1.6.1'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

group = 'net.sourceforge.pcgen'

description = """PCGen"""

defaultTasks 'clean', 'build'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    binDir = "code/bin"

    // Default ignoreTestFailures to false if it is not provided
    if (!hasProperty('ignoreTestFailures')) {
        ignoreTestFailures = false;
    }

}

mainClassName = 'pcgen.system.Main'
applicationDistribution.from('plugins') {
    into "plugins"
}
applicationDistribution.from('docs') {
    into "docs"
}
applicationDistribution.from('data') {
    into "data"
}
applicationDistribution.from('outputsheets') {
    into "outputsheets"
}
applicationDistribution.from('system') {
    into "system"
}
applicationDistribution.from('preview') {
    into "preview"
}

repositories {
    mavenCentral()
    ivy {
        url "http://pcgen.sourceforge.net/mvnrepo"
        layout "pattern", {
            artifact "[organisation]/jars/[artifact]-[revision].[ext]"
        }
    }
    ivy {
        name "fileRepo"
        url 'http://librepo.pcgen.org'
    }
    jcenter()
}

sourceSets {
    main {
        java {
            srcDirs 'code/src/java'
        }
        resources {
            srcDirs = ['code/src/java', 'code/resources' ]
            include '**/*.properties'
            include '**/*.xml'
            include '**/*.gif'
            include '**/*.png'
            include '**/*.jpg'
        }
    }
    test {
        java {
            srcDirs = ['code/src/utest', 'code/src/testcommon']
        }
    }
    itest {
        java {
            srcDirs = ['code/src/itest', 'code/src/testcommon']
        }
    }
    slowtest {
        java {
            srcDirs = ['code/src/test', 'code/src/testcommon']
        }
    }
}

/* Copy 'master' outputsheets into different genre folders */
task copyMasterSheets(type: Copy) {
    outputs.upToDateWhen { false }

    include 'eqsheet_fantasy_std.htm'
    include 'psheet_fantasy_std.htm'
    from 'outputsheets/d20/fantasy/htmlxml'
    into 'outputsheets/d20/historical/htmlxml'

    into ('../../horror/htmlxml') {
        from 'outputsheets/d20/fantasy/htmlxml'
    }
    into ('../../sciencefiction/htmlxml') {
        from 'outputsheets/d20/fantasy/htmlxml'
    }
    into ('../../western/htmlxml') {
        from 'outputsheets/d20/fantasy/htmlxml'
    }
}

compileJava {
    it.dependsOn 'copyMasterSheets'
}


dependencies {
    compile group: 'cobra', name: 'cobra', version:'0.98.4-pcgen'

    compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
    compile group: 'commons-io', name: 'commons-io', version:'2.5'
    compile group: 'rhino', name: 'js', version:'1.7R2'
    compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
    compile group: 'commons-logging', name: 'commons-logging', version:'1.1.1'
    compile group: 'org.springframework', name: 'spring-core', version:'4.3.2.RELEASE'
    compile group: 'org.springframework', name: 'spring-beans', version:'4.3.2.RELEASE'
    compile group: 'skinlf', name: 'skinlf', version: '1.2.3'
    compile group: 'org.apache.xmlgraphics', name: 'fop', version:'2.1'
    compile group: 'org.apache.pdfbox', name: 'fontbox', version:'2.0.2'
    compile group: 'jep', name: 'jep', version:'2.4.1'
    compile group: 'org.freemarker', name: 'freemarker', version:'2.3.23'
    compile group: 'org.jdom', name: 'jdom2', version:'2.0.6'
    compile group: 'xalan', name: 'xalan', version:'2.7.2'
    compile group: 'xerces', name: 'xercesImpl', version:'2.11.0'
    compile group: 'xml-apis', name: 'xml-apis', version:'1.4.01'
    compile group: 'com.incors.plaf', name: 'kunststoff', version:'2.0.2'
    compile group: 'net.sourceforge.argparse4j', name: 'argparse4j', version: '0.7.0'
    compile group: 'net.sourceforge.pcgen', name: 'PCGen-base', version:'1.0.83'
    compile group: 'net.sourceforge.pcgen', name: 'PCGen-Formula', version:'1.0.95'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'

    runtime group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.6'

    compileOnly group: 'org.jetbrains', name: 'annotations', version:'15.0'
    compileOnly group: 'com.yuvimasory', name: 'orange-extensions', version: '1.3.0'
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'

    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'xmlunit', name: 'xmlunit', version:'1.6'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'

    itestCompile sourceSets.main.output
    itestCompile configurations.testCompile
    itestCompile sourceSets.test.output

    itestRuntime configurations.testRuntime

    slowtestCompile sourceSets.main.output
    slowtestCompile configurations.testCompile
    slowtestCompile sourceSets.test.output

    slowtestRuntime configurations.testRuntime

    findbugs 'com.google.code.findbugs:findbugs:3.0.1'
    findbugs configurations.findbugsPlugins.dependencies

    findbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:6.6.2'
}

ant.importBuild 'build-gradle.xml'
ant.properties['src.java.dir']="code/src/java"
ant.properties['build.classes.dir']="build/classes/main"

configure('jar-all-plugins') {
    group = BasePlugin.BUILD_GROUP // Or use 'build'
    description = 'Create the plugin jars'
}

ext {
    classpath = ""
    configurations.runtime.each { lib -> classpath += " libs/${lib.name} "}
}

jar {
    it.dependsOn 'jar-all-plugins'
    manifest {
        attributes 'Implementation-Title': 'PCGen', 'Implementation-Version': version,
        	'Main-Class': 'pcgen.system.Main', 'Class-Path': classpath
    }
}

task converterJar(type: Jar, dependsOn: jar) {
    manifest {
        attributes 'Implementation-Title': 'PCGen Data Converter', 'Implementation-Version': version,
        	'Main-Class': 'pcgen.gui2.converter.PCGenDataConvert', 'Class-Path': 'pcgen.jar' + classpath
    }

    classifier = 'batch-convert'
    from sourceSets.main.output
    include 'none'
}

artifacts {
    archives converterJar
}

task copyToLibs(type: Copy) {
    into "$buildDir/libs"
    from configurations.runtime
}

task copyToRoot(type: Copy, dependsOn: [copyToLibs, jar, converterJar]) {
    outputs.files.setFrom(file("$buildDir/libs/pcgen-${version}.jar"))

    from "$buildDir/libs/pcgen-${version}.jar"
    from "$buildDir/libs/pcgen-${version}-batch-convert.jar"
    from "$buildDir/release/pcgen.exe"
    from "$projectDir/code/pcgen.sh"
    into "$projectDir"
    rename "(.+)-$version(.+)", '$1$2'
}

task syncLibsToRoot(type: Sync, dependsOn: [copyToLibs, jar, converterJar]) {
    outputs.files.setFrom(file("$buildDir/libs/pcgen-${version}.jar"))
    outputs.dir("$projectDir/libs")
    inputs.files(fileTree("$buildDir/libs"))

    from "${buildDir}/libs"
    into "$projectDir/libs"
    exclude "pcgen-*.jar"
}

task cleanRoot(type: Delete) {
    description="Clean up things copied to the root folder by the build"
    delete "$projectDir/pcgen.jar"
    delete "$projectDir/pcgen-batch-convert.jar"
    delete "$projectDir/batch-convert.jar"
    delete "$projectDir/pcgen.sh"
    delete "$projectDir/pcgen.exe"
    delete "$projectDir/libs"
    delete "$projectDir/autobuild.properties"
    delete "$projectDir/svn.properties"
}

build {
    it.dependsOn 'copyToRoot', 'syncLibsToRoot'
}

clean {
    it.dependsOn 'clean-plugins'
    it.dependsOn 'cleanRoot'
}

test {
    exclude 'pcgen/testsupport/**'
    //	maxParallelForks 4
    testLogging {
        exceptionFormat = 'full'
    }
}

task itest(type: Test, dependsOn: jar) {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath
    ignoreFailures=ignoreTestFailures
    testLogging {
        exceptionFormat = 'full'
    }
}

task slowtest(type: Test, dependsOn: ['jar', 'itest']) {
    testClassesDir = sourceSets.slowtest.output.classesDir
    classpath = sourceSets.slowtest.runtimeClasspath
    systemProperties['jar.path'] = jar.archivePath
    forkEvery = 1
    maxHeapSize = "512m"
    // Exclude the two suites to avoid duplicate test runs.
    exclude 'AllJUnitTests.class'
    exclude 'pcgen/core/AllJUnitTests.class'
    ignoreFailures=ignoreTestFailures
    testLogging {
        exceptionFormat = 'full'
    }
}

task datatest(type: Test, dependsOn: 'jar') {
    testClassesDir = sourceSets.slowtest.output.classesDir
    classpath = sourceSets.slowtest.runtimeClasspath
    include 'pcgen/persistence/lst/DataTest.class'
    include 'pcgen/persistence/lst/DataLoadTest.class'
    ignoreFailures=ignoreTestFailures
    maxHeapSize = "512m"
    testLogging {
        exceptionFormat = 'full'
    }
}

task inttest(type: Test, dependsOn: 'jar') {
    testClassesDir = sourceSets.slowtest.output.classesDir
    classpath = sourceSets.slowtest.runtimeClasspath
    forkEvery = 1
    maxHeapSize = "512m"
    include 'pcgen/inttest/**/*Test.class'
    testLogging {
        exceptionFormat = 'full'
    }

    // Report each test as it is started.
    beforeTest { descriptor ->
    	logger.lifecycle("Running test: " + descriptor)
    }
}

task pfinttest(type: Test, dependsOn: 'jar') {
    testClassesDir = sourceSets.slowtest.output.classesDir
    classpath = sourceSets.slowtest.runtimeClasspath
    forkEvery = 1
    maxHeapSize = "512m"
    include 'pcgen/inttest/game_pathfinder/*Test.class'
    testLogging {
        exceptionFormat = 'full'
    }

    // Report each test as it is started.
    beforeTest { descriptor ->
    	logger.lifecycle("Running test: " + descriptor)
    }
}

task rsrdinttest(type: Test, dependsOn: 'jar') {
    testClassesDir = sourceSets.slowtest.output.classesDir
    classpath = sourceSets.slowtest.runtimeClasspath
    forkEvery = 1
    maxHeapSize = "512m"
    include 'pcgen/inttest/game_35e/*Test.class'
    testLogging {
        exceptionFormat = 'full'
    }

    // Report each test as it is started.
    beforeTest { descriptor ->
    	logger.lifecycle("Running test: " + descriptor)
    }
}

task msrdinttest(type: Test, dependsOn: 'jar') {
    testClassesDir = sourceSets.slowtest.output.classesDir
    classpath = sourceSets.slowtest.runtimeClasspath
    forkEvery = 1
    maxHeapSize = "512m"
    include 'pcgen/inttest/game_modern/*Test.class'
    testLogging {
        exceptionFormat = 'full'
    }

    // Report each test as it is started.
    beforeTest { descriptor ->
    	logger.lifecycle("Running test: " + descriptor)
    }
}

// Do the lot!
task all(dependsOn: ['clean', 'build', 'slowtest', 'javadoc', 'allReports']) {
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

//Disable failing on javadoc warning
allprojects {
    tasks.withType(Javadoc) {
        options.addBooleanOption('Xdoclint:none', true)
    }
}

apply from: 'code/gradle/autobuild.gradle'
apply from: 'code/gradle/distribution.gradle'
apply from: 'code/gradle/reporting.gradle'
apply from: 'code/gradle/release.gradle'

