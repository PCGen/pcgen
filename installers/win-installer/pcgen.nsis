; Last Editor: $Author: jdempsey $
; Last Edited: $Date: 2013-11-30 13:35:43 +1100 (Sat, 30 Nov 2013) $

; Known issues

; Outstanding Issues
;		Need file association for .pcg files
;		java -Dpcgen.inputfile=Ilse.pcg -Dpcgen.outputfile=Ilse.pdf -Dpcgen.templatefile=csheet_fantasy_std_blackandwhite.xslt -jar pcgen.jar


; Begin Script ----------------------------------------------------------------------------
; Include the externally defined constants
!include "project.nsh"
!define INCLUDES_DIR "${PROJECT_BUILD_DIR}\..\installers\win-installer\includes"
!include ${INCLUDES_DIR}\constants.nsh
;To detect 64 vs. 32 bit. Used for installing right JRE version if needed.
!include x64.nsh
;File association
!include ${INCLUDES_DIR}\FileAssociation.nsh

; Define constants
!define APPNAME "PCGen"
!define APPNAMEANDVERSION "${APPNAME} ${LONGVER}"
!define APPDIR "${LONGVER}"
!define TargetVer "1.7"
!define OverVer "1.9"
!define OutName "pcgen${SIMPVER}_win_install"


; Main Install settings
Name "${APPNAMEANDVERSION}"
InstallDir "$LOCALAPPDATA\${APPNAME}"
InstallDirRegKey HKLM "Software\${APPNAME}\${APPDIR}" ""
OutFile "${OutDir}\${OutName}.exe"
;This will save a little less than 1mb, it should be left enabled -Ed
SetCompressor lzma
;This will force the installer to do a CRC check prior to install,
;it is safer, so should be left on. -Ed
CRCCheck on

; Install Type Settings
InstType "Full Install"
InstType "Average Install"
InstType "Average All SRD"
InstType "Min - SRD"
InstType "Min - SRD 3.5"
InstType "Min - MSRD"

;	Look and style
ShowInstDetails show
InstallColors FF8080 000030
XPStyle on
Icon "${SrcDir}\Local\PCGen2.ico"


; Modern interface settings
!include "MUI.nsh"

; if/then/else etc
!include 'LogicLib.nsh'

!define MUI_ABORTWARNING
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "${SrcDir}\PCGen_${SIMPVER}_base\docs\acknowledgments\PCGenLicense.txt"
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

; Set languages (first is default language)
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_RESERVEFILE_LANGDLL

Section "PCGen" Section1

	SectionIn RO

	; Set Section properties
	SetOverwrite ifnewer

	; Set Section Files and Shortcuts
	SetOutPath "$INSTDIR\${APPDIR}\"
	File /r "${SrcDir}\PCGen_${SIMPVER}_base\*.*"

SectionEnd

SubSection /e "Data" Section2

# Run the perl script gendatalist.pl to generate the file below.
!include ${INCLUDES_DIR}\data.nsh

SubSectionEnd

SubSection /e "PlugIns" Section3

	Section "Skins"

	SectionIn 1 2 3
	SetOutPath "$INSTDIR\${APPDIR}\libs"
	File /r "${SrcDir}\PCGen_${SIMPVER}_opt\plugin\skin\libs\*.*"

	SectionEnd

	Section "PDF"

	SectionIn 1 2 3
	SetOutPath "$INSTDIR\${APPDIR}\libs"
	File /r "${SrcDir}\PCGen_${SIMPVER}_opt\plugin\pdf\libs\*.*"
	SetOutPath "$INSTDIR\${APPDIR}\outputsheets"
	File /r "${SrcDir}\PCGen_${SIMPVER}_opt\plugin\pdf\outputsheets\*.*"

	SectionEnd

	Section "GMGen Plugins"

	SectionIn 1 2 3
	SetOutPath "$INSTDIR\${APPDIR}\plugins"
	File /r "${SrcDir}\PCGen_${SIMPVER}_opt\plugin\gmgen\plugins\*.*"

	SectionEnd

SubSectionEnd

Section "-Local" Section4

	; Set Section properties
	SetOverwrite ifnewer

	; Set Section Files and Shortcuts
	SetOutPath "$INSTDIR\${APPDIR}\Local\"
	File /r "${SrcDir}\Local\*.*"

	; Create Shortcuts
	SetOutPath "$INSTDIR\${APPDIR}\"
	CreateDirectory "$SMPROGRAMS\PCGen\${APPDIR}"
	CreateShortCut "$DESKTOP\${APPDIR}.lnk" "$INSTDIR\${APPDIR}\pcgen.exe" "" \
				"$INSTDIR\${APPDIR}\Local\PCGen2.ico" 0 SW_SHOWMINIMIZED
# We no longer provide the .bat file.
#	CreateShortCut "$SMPROGRAMS\PCGEN\${APPDIR}\${APPDIR}-Low.lnk" "$INSTDIR\${APPDIR}\pcgen_low_mem.bat" "" \
#				"$INSTDIR\${APPDIR}\Local\PCGen.ico" 0 SW_SHOWMINIMIZED
	CreateShortCut "$SMPROGRAMS\PCGEN\${APPDIR}\${APPDIR}.lnk" "$INSTDIR\${APPDIR}\pcgen.exe" "" \
				"$INSTDIR\${APPDIR}\Local\pcgen2.ico" 0 SW_SHOWMINIMIZED
	CreateShortCut "$SMPROGRAMS\PCGen\${APPDIR}\Convert Data.lnk" "$$INSTDIR\${APPDIR}\jre\bin\javaw.exe" "-Xmx256M -jar batch-convert.jar" \
				"$INSTDIR\${APPDIR}\Local\convert.ico"
	CreateShortCut "$SMPROGRAMS\PCGen\${APPDIR}\Release Notes.lnk" "$INSTDIR\${APPDIR}\pcgen-release-notes-${SIMPVER}.html" "" "$INSTDIR\${APPDIR}\Local\knight.ico"
	CreateShortCut "$SMPROGRAMS\PCGen\${APPDIR}\News.lnk" "http://pcgen.sourceforge.net/02_news.php" "" "$INSTDIR\${APPDIR}\Local\queen.ico"
	CreateShortCut "$SMPROGRAMS\PCGen\${APPDIR}\uninstall-${APPDIR}.lnk" "$INSTDIR\uninstall-${APPDIR}.exe"
	CreateShortCut "$SMPROGRAMS\PCGen\${APPDIR}\Manual.lnk" "$INSTDIR\${APPDIR}\docs\index.html" "" "$INSTDIR\${APPDIR}\Local\castle.ico"
        ;Add file extension registration
        ;File association. See: http://nsis.sourceforge.net/FileAssoc
        !insertmacro APP_ASSOCIATE "pcg" "PCGen.File" "PCGen Character file" \
                 "$INSTDIR\${APPDIR}\pcgen.exe,0" "Open with PCGen" "$INSTDIR\${APPDIR}\pcgen.exe $\"%1$\""
        System::Call 'Shell32::SHChangeNotify(i ${SHCNE_ASSOCCHANGED}, i 0, i 0, i 0)'

SectionEnd

Section "JRE" Section5
        SectionIn RO

        ;Use the right java version
        DetailPrint "JRE extraction..."
        SetOutPath "$INSTDIR\${APPDIR}\jre"
        ${If} ${RunningX64}
            File /nonfatal /a /r "${SrcDir}\jre\jre_64\*.*"
        ${Else}
            File /nonfatal /a /r "${SrcDir}\jre\jre_32\*.*"
        ${EndIf} 
        DetailPrint "JRE extraction complete!"
SectionEnd

Section -FinishSection

	WriteRegStr HKLM "Software\${APPNAME}\${APPDIR}" "" "$INSTDIR\${APPDIR}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPDIR}" "DisplayName" "${APPDIR}"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPDIR}" "UninstallString" "$INSTDIR\uninstall-${APPDIR}.exe"
	WriteUninstaller "$INSTDIR\uninstall-${APPDIR}.exe"

SectionEnd

; Modern install component descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
	!insertmacro MUI_DESCRIPTION_TEXT ${Section1} "This is the PCGen Core"
	!insertmacro MUI_DESCRIPTION_TEXT ${Section2} "This section installs the data sets you need"
	!insertmacro MUI_DESCRIPTION_TEXT ${Section3} "This section installs the plug ins you may need"
	!insertmacro MUI_DESCRIPTION_TEXT ${Section4} "This is for icons and such"
        !insertmacro MUI_DESCRIPTION_TEXT ${Section5} "This is the embedded JRE used by PCGen"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Section Uninstall

	; Delete self
	Delete "$INSTDIR\uninstall-${APPDIR}.exe"

	; Remove from registry...
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPDIR}"
	DeleteRegKey HKLM "Software\${APPNAME}\${APPDIR}"
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${APPDIR}_alpha"

	; Delete Desktop Shortcut
	Delete "$DESKTOP\${APPDIR}.lnk"
	; Delete Shortcut Directory
	RMDir /r "$SMPROGRAMS\PCGen\${APPDIR}"
        ;Delete file extension registration
        !insertmacro APP_UNASSOCIATE "pcg" "PCGen.File"

	MessageBox MB_YESNO "Do you wish to save, your characters, custom sources etc? $\nAnswering no will delete ${APPDIR}." IDYES Save IDNO NoSave

	Save:
	CreateDirectory "$INSTDIR\${APPDIR}_Save"
	CreateDirectory "$INSTDIR\${APPDIR}_Save\characters"
	CreateDirectory "$INSTDIR\${APPDIR}_Save\customsources"
	CreateDirectory "$INSTDIR\${APPDIR}_Save\settings"
	CreateDirectory "$INSTDIR\${APPDIR}_Save\GMGen"
	CopyFiles /SILENT "$INSTDIR\${APPDIR}\characters\*.*" 			"$INSTDIR\${APPDIR}_Save\characters\"
	CopyFiles /SILENT "$INSTDIR\${APPDIR}\data\customsources\*.*" 	"$INSTDIR\${APPDIR}_Save\customsources\"
	CopyFiles /SILENT "$INSTDIR\${APPDIR}\*.ini" 						"$INSTDIR\${APPDIR}_Save\"
	CopyFiles /SILENT "$INSTDIR\${APPDIR}\settings\*.*" 				"$INSTDIR\${APPDIR}_Save\settings\"
	;Ed- This has not been tested, Please test.
	CopyFiles /SILENT "$INSTDIR\${APPDIR}\plugins\Notes\*.*" 				"$INSTDIR\${APPDIR}_Save\GMGen\"
	MessageBox MB_ICONINFORMATION|MB_OK "A shortcut will be created on your desktop to the saved files."
	CreateShortCut "$DESKTOP\${APPDIR}_Save.lnk" "$INSTDIR\${APPDIR}_Save"

	NoSave:
	; Clean up PCGen program directory by deleting folders.
	;Ed- This method is used, as a safer alternative
	RMDir /r "$INSTDIR\${APPDIR}\characters"
	RMDir /r "$INSTDIR\${APPDIR}\data"
	RMDir /r "$INSTDIR\${APPDIR}\docs"
	RMDir /r "$INSTDIR\${APPDIR}\libs"
        ;Remove local JRE
        RMDir /r "$INSTDIR\${APPDIR}\jre"
	RMDir /r "$INSTDIR\${APPDIR}\Local"
	RMDir /r "$INSTDIR\${APPDIR}\outputsheets"
	RMDir /r "$INSTDIR\${APPDIR}\plugins"
	RMDir /r "$INSTDIR\${APPDIR}\preview"
	RMDir /r "$INSTDIR\${APPDIR}\system"
	RMDir /r "$INSTDIR\${APPDIR}\settings"
	;Ed- below would be the removal of all files in the PCGen root directory, on a file by file basis.
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\pcgen.jar"
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\pcgen-release-notes-${SIMPVER}.html"
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\pcgen.exe"
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\pcgen.sh"
#	Delete /REBOOTOK "$INSTDIR\${APPDIR}\pcgen_low_mem.bat"
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\pcgen-batch-convert.jar"
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\filepaths.ini"
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\config.ini"
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\logging.properties"
	Delete /REBOOTOK "$INSTDIR\${APPDIR}\pcgen.log"
	
	RMDir "$INSTDIR\${APPDIR}"

SectionEnd

; eof
